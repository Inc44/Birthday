000001*cobc -x -O3 -o birthday birthday.cob && ./birthday
000002 IDENTIFICATION DIVISION.
000003 PROGRAM-ID. BIRTHDAY.
000004 DATA DIVISION.
000005 WORKING-STORAGE SECTION.
000006 01 CONSTANTS.
000007    05 DAYS-IN-YEAR            PIC 9(003)        VALUE 365.
000008    05 NUM-THREADS             PIC 9(003)        VALUE 768.
000009    05 PEOPLE                  PIC 9(002)        VALUE 24.
000010    05 TOTAL-SIMULATIONS       PIC 9(010)        VALUE 1000000.
000011    05 MULTIPLIER              PIC 9(007)        VALUE 1664525.
000012    05 INCREMENT               PIC 9(010)        VALUE 1013904223.
000013    05 UINT32-T                PIC 9(010)        VALUE 4294967296.
000014    05 BUFFER-SIZE             PIC 9(009) COMP-5 VALUE 4.
000015 01 VARIABLES.
000016    05 SEED                    PIC 9(010) COMP-5.
000017    05 STATE                   PIC 9(010) COMP-5.
000018    05 LOCAL-SUCCESS-COUNT     PIC 9(010) COMP-5.
000019    05 I                       PIC 9(003) COMP-5.
000020    05 EXACTLY-TWO-COUNT       PIC 9(002) COMP-5.
000021    05 BIRTHDAY                PIC 9(003) COMP-5.
000022    05 TOTAL-SUCCESS-COUNT     PIC 9(010) COMP-5 VALUE 0.
000023    05 BIRTHDAYS-TABLE.
000024       10 BIRTHDAYS            PIC 9(002) COMP-5
000025             OCCURS 365 TIMES.
000026 01 PARALLEL-VARS.
000027    05 THREAD-ID               PIC 9(003) COMP-5.
000028    05 SIMULATIONS-PER-THREAD  PIC 9(010) COMP-5.
000029    05 T                       PIC 9(003) COMP-5.
000030    05 PIPE.
000031       10 STDIN                PIC 9(009) COMP-5.
000032       10 STDOUT               PIC 9(009) COMP-5.
000033    05 SUCCESS_COUNT-TABLE.
000034       10 SUCCESS_COUNT        PIC 9(010) COMP-5
000035             OCCURS 768 TIMES.
000036 01 TIMING.
000037    05 SEED-DT                 PIC X(016).
000038    05 START-DT                PIC X(016).
000039    05 END-DT                  PIC X(016).
000040    05 START-TIME              PIC 9(006)V9(002).
000041    05 END-TIME                PIC 9(006)V9(002).
000042    05 ELAPSED-TIME            PIC 9(006)V9(002).
000043    05 PROBABILITY             PIC 9(001)V9(009).
000044    05 DISPLAY-PROB            PIC 0.9(009).
000045    05 DISPLAY-TIME            PIC Z(006)9.9(003).
000046 PROCEDURE DIVISION.
000047 SIMULATE.
000048     PERFORM SIMULATIONS-PER-THREAD TIMES
000049             INITIALIZE BIRTHDAYS-TABLE
000050             PERFORM PEOPLE TIMES
000051                     COMPUTE STATE = FUNCTION MOD((STATE *
000052                        MULTIPLIER + INCREMENT) UINT32-T)
000053                     COMPUTE BIRTHDAY = FUNCTION MOD(STATE
000054                        DAYS-IN-YEAR) + 1
000055                     ADD 1 TO BIRTHDAYS(BIRTHDAY)
000056             END-PERFORM
000057             MOVE 0 TO EXACTLY-TWO-COUNT
000058             PERFORM VARYING I FROM 1 BY 1 UNTIL I > DAYS-IN-YEAR
000059                     IF BIRTHDAYS(I) = 2
000060                        ADD 1 TO EXACTLY-TWO-COUNT
000061                     END-IF
000062             END-PERFORM
000063             IF EXACTLY-TWO-COUNT = 1
000064                ADD 1 TO LOCAL-SUCCESS-COUNT
000065             END-IF
000066     END-PERFORM.
000067 MAIN.
000068     MOVE FUNCTION CURRENT-DATE TO START-DT.
000069     COMPUTE START-TIME =
000070        FUNCTION NUMVAL(START-DT(9:2)) * 3600 +
000071        FUNCTION NUMVAL(START-DT(11:2)) * 60 +
000072        FUNCTION NUMVAL(START-DT(13:2)) +
000073        FUNCTION NUMVAL(START-DT(15:2)) / 100.
000074     COMPUTE SIMULATIONS-PER-THREAD = TOTAL-SIMULATIONS /
000075        NUM-THREADS.
000076     PERFORM VARYING T FROM 1 BY 1 UNTIL T > NUM-THREADS
000077             CALL "pipe" USING BY REFERENCE PIPE
000078             MOVE STDIN TO SUCCESS_COUNT(T)
000079             CALL "fork" RETURNING THREAD-ID
000080             IF THREAD-ID = 0
000081                CALL "close" USING BY VALUE STDIN
000082                CALL "getpid" RETURNING THREAD-ID
000083                MOVE FUNCTION CURRENT-DATE TO SEED-DT
000084                COMPUTE SEED =
000085                   FUNCTION NUMVAL(SEED-DT(9:2)) * 3600 +
000086                   FUNCTION NUMVAL(SEED-DT(11:2)) * 60 +
000087                   FUNCTION NUMVAL(SEED-DT(13:2)) +
000088                   FUNCTION NUMVAL(SEED-DT(15:2)) / 100 +
000089                   THREAD-ID
000090                MOVE SEED TO STATE
000091                MOVE 0 TO LOCAL-SUCCESS-COUNT
000092                PERFORM SIMULATE
000093                CALL "write" USING BY VALUE STDOUT
000094                                   BY REFERENCE
000095                   LOCAL-SUCCESS-COUNT
000096                                   BY VALUE BUFFER-SIZE
000097                CALL "close" USING BY VALUE STDOUT
000098                STOP RUN
000099             ELSE
000100                CALL "close" USING BY VALUE STDOUT
000101             END-IF
000102     END-PERFORM.
000103     PERFORM VARYING T FROM 1 BY 1 UNTIL T > NUM-THREADS
000104             CALL "read" USING BY VALUE SUCCESS_COUNT(T)
000105                               BY REFERENCE LOCAL-SUCCESS-COUNT
000106                               BY VALUE BUFFER-SIZE
000107             ADD LOCAL-SUCCESS-COUNT TO TOTAL-SUCCESS-COUNT
000108             CALL "close" USING BY VALUE SUCCESS_COUNT(T)
000109             CALL "wait" USING BY VALUE 0
000110     END-PERFORM.
000111     MOVE FUNCTION CURRENT-DATE TO END-DT.
000112     COMPUTE END-TIME =
000113        FUNCTION NUMVAL(END-DT(9:2)) * 3600 +
000114        FUNCTION NUMVAL(END-DT(11:2)) * 60 +
000115        FUNCTION NUMVAL(END-DT(13:2)) +
000116        FUNCTION NUMVAL(END-DT(15:2)) / 100.
000117     COMPUTE ELAPSED-TIME = END-TIME - START-TIME.
000118     COMPUTE PROBABILITY = TOTAL-SUCCESS-COUNT / TOTAL-SIMULATIONS.
000119     MOVE PROBABILITY TO DISPLAY-PROB.
000120     DISPLAY "Probability: " DISPLAY-PROB.
000121     MOVE ELAPSED-TIME TO DISPLAY-TIME.
000122     DISPLAY "Execution Time: " FUNCTION TRIM(DISPLAY-TIME) " s".
000123     STOP RUN.
