000001*cobc -x -O3 -o birthday birthday.cob && ./birthday
000002 IDENTIFICATION DIVISION.
000003 PROGRAM-ID. BIRTHDAY.
000004 DATA DIVISION.
000005 WORKING-STORAGE SECTION.
000006 01 CONSTANTS.
000007    05 DAYS-IN-YEAR         PIC 9(003)         VALUE 365.
000008    05 PEOPLE               PIC 9(002)         VALUE 24.
000009    05 TOTAL-SIMULATIONS    PIC 9(010)         VALUE 1000000.
000010    05 MULTIPLIER           PIC 9(007)         VALUE 1664525.
000011    05 INCREMENT            PIC 9(010)         VALUE 1013904223.
000012    05 UINT32-T             PIC 9(010)         VALUE 4294967296.
000013 01 VARIABLES.
000014    05 SEED                 PIC 9(010) COMP-5.
000015    05 STATE                PIC 9(010) COMP-5.
000016    05 EXACTLY-TWO-COUNT    PIC 9(002) COMP-5.
000017    05 I                    PIC 9(003) COMP-5.
000018    05 BIRTHDAY             PIC 9(003) COMP-5.
000019    05 TOTAL-SUCCESS-COUNT  PIC 9(010) COMP-5  VALUE 0.
000020    05 BIRTHDAYS-TABLE.
000021       10 BIRTHDAYS         PIC 9(002) COMP-5 OCCURS 365 TIMES.
000022 01 TIMING.
000023    05 SEED-DT              PIC X(016).
000024    05 START-DT             PIC X(016).
000025    05 END-DT               PIC X(016).
000026    05 START-TIME           PIC 9(006)V9(002).
000027    05 END-TIME             PIC 9(006)V9(002).
000028    05 ELAPSED-TIME         PIC 9(006)V9(002).
000029    05 PROBABILITY          PIC 9(001)V9(009).
000030    05 DISPLAY-PROB         PIC 0.9(009).
000031    05 DISPLAY-TIME         PIC Z(006)9.9(003).
000032 PROCEDURE DIVISION.
000033 SIMULATE.
000034     INITIALIZE BIRTHDAYS-TABLE.
000035     PERFORM PEOPLE TIMES
000036             COMPUTE STATE = FUNCTION MOD((STATE * MULTIPLIER +
000037                INCREMENT) UINT32-T)
000038             COMPUTE BIRTHDAY = FUNCTION MOD(STATE DAYS-IN-YEAR) +
000039                1
000040             ADD 1 TO BIRTHDAYS(BIRTHDAY)
000041     END-PERFORM.
000042     MOVE 0 TO EXACTLY-TWO-COUNT.
000043     PERFORM VARYING I FROM 1 BY 1 UNTIL I > DAYS-IN-YEAR
000044             IF BIRTHDAYS(I) = 2
000045                ADD 1 TO EXACTLY-TWO-COUNT
000046             END-IF
000047     END-PERFORM.
000048     IF EXACTLY-TWO-COUNT = 1
000049        ADD 1 TO TOTAL-SUCCESS-COUNT
000050     END-IF.
000051 MAIN.
000052     MOVE FUNCTION CURRENT-DATE TO START-DT.
000053     COMPUTE START-TIME = FUNCTION NUMVAL(START-DT(9:2)) * 3600 +
000054        FUNCTION NUMVAL(START-DT(11:2)) * 60 +
000055        FUNCTION NUMVAL(START-DT(13:2)) +
000056        FUNCTION NUMVAL(START-DT(15:2)) / 100.
000057     MOVE FUNCTION CURRENT-DATE TO SEED-DT.
000058     COMPUTE SEED = FUNCTION NUMVAL(SEED-DT(9:2)) * 3600 +
000059        FUNCTION NUMVAL(SEED-DT(11:2)) * 60 +
000060        FUNCTION NUMVAL(SEED-DT(13:2)) +
000061        FUNCTION NUMVAL(SEED-DT(15:2)) / 100.
000062     MOVE SEED TO STATE.
000063     PERFORM SIMULATE TOTAL-SIMULATIONS TIMES.
000064     MOVE FUNCTION CURRENT-DATE TO END-DT.
000065     COMPUTE END-TIME = FUNCTION NUMVAL(END-DT(9:2)) * 3600 +
000066        FUNCTION NUMVAL(END-DT(11:2)) * 60 +
000067        FUNCTION NUMVAL(END-DT(13:2)) +
000068        FUNCTION NUMVAL(END-DT(15:2)) / 100.
000069     COMPUTE ELAPSED-TIME = END-TIME - START-TIME.
000070     COMPUTE PROBABILITY = TOTAL-SUCCESS-COUNT / TOTAL-SIMULATIONS.
000071     MOVE PROBABILITY TO DISPLAY-PROB.
000072     DISPLAY "Probability: " DISPLAY-PROB.
000073     MOVE ELAPSED-TIME TO DISPLAY-TIME.
000074     DISPLAY "Execution Time: " FUNCTION TRIM(DISPLAY-TIME) " s".
000075     STOP RUN.
